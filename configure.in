AC_INIT(desktop-properties)

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(gnome-core, 0.28)

AM_MAINTAINER_MODE

AM_ACLOCAL_INCLUDE(macros)

dnl
dnl let applications configure for gnome
dnl
gnome_cv_use_gnome=yes

GNOME_INIT

AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS
AC_ARG_PROGRAM
AM_PROG_LIBTOOL
AC_PROG_CXX
AM_PROG_LEX
AC_PROG_YACC

GNOME_X_CHECKS

dnl utility conditional
AM_CONDITIONAL(FALSE, test "x" = "y")

ALL_LINGUAS="it ko fr de es no ga sv pt ja fi"
AM_GNU_GETTEXT

AC_LINK_FILES($nls_cv_header_libgt, $nls_cv_header_intl)

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)


AC_CHECK_HEADERS(dlfcn.h dl.h)
AC_CHECK_LIB(dl, dlopen, DL_LIB="-ldl",[
AC_CHECK_LIB(dld, shl_load, DL_LIB="-ldld",[
AC_CHECK_FUNCS(dlopen, DL_LIB="",
AC_MSG_ERROR(Dynamic linking is not available on this platform.  Some
apps, like panel, will not run properly.))
])])
AC_SUBST(DL_LIB)

dnl properties-keyboard
AC_CHECK_HEADERS(X11/extensions/xf86misc.h, XF86MISC_LIBS="-lXxf86misc")
AC_SUBST(XF86MISC_LIBS)

for dir in `(IFS=:; for i in $PATH; do echo $i ; done)` ; do
  if test -f $dir/orbit-idl ; then
     orbit_prefix=`echo $dir | sed 's%/bin$%%' |sed 's%/bin/$%%'`
     break
  fi
done
AC_CHECK_PROG(ORBIT_CONFIG, orbit-config, orbit-config)
if test -n "$ORBIT_CONFIG"; then
	ORBIT_LIBS="`orbit-config --libs client server`"
	ORBIT_CFLAGS="`orbit-config --cflags client server`"
fi
AC_SUBST(ORBIT_CFLAGS)
AC_SUBST(ORBIT_LIBS)
AM_CONDITIONAL(ORBIT_INSTALLED, test -n "$ORBIT_LIBS")

ORB_LIBS="$ORBIT_LIBS"
ORB_CFLAGS="$ORBIT_CFLAGS"

dnl MICO - Magic to extract the relevant information from
dnl        various installed files
MICO_LIBS= MICO_CFLAGS=
if test -z "$ORB_LIBS"; then
  AC_MSG_CHECKING(for MICO prefix)
  for dir in `(IFS=:; for i in $PATH; do echo $i ; done)` ; do
    if test -f $dir/mico-ld ; then
       mico_prefix=`echo $dir | sed 's%/bin$%%' |sed 's%/bin/$%%'`
       break
    fi
  done

  if test x${mico_prefix+set} = xset ; then
    AC_MSG_RESULT($mico_prefix)
  else
    AC_MSG_RESULT(not found)
    AC_MSG_ERROR(MICO not found)
  fi

  AC_MSG_CHECKING(for MICO version)
  if test -f "$mico_prefix/lib/mico-setup.sh"; then
     MICO_VERS=`(. $mico_prefix/lib/mico-setup.sh ; echo $MICOVERSION)`
  else
     # Sometimes mico-setup.sh is installed in doc.
     files="`echo $mico_prefix/doc/mico-*/mico-setup.sh`"
     if test "$files" != "$mico_prefix/doc/mico-*/mico-setup.sh"; then
	MICO_VERS=`(set X $files; . $2; echo $MICOVERSION)`
     else
	AC_MSG_ERROR(unknown)
     fi
  fi
  AC_MSG_RESULT($MICO_VERS)

  # We assume here, to keep things simple, that nobody has MICO older
  # than 2.0.3.  2.0.4 is not checked for since nobody seems to use it.

  if test $MICO_VERS = 2.0.3 || test $MICO_VERS = 2.0.5; then
    MICO_LIBS="-lmicoaux$MICO_VERS -lmico$MICO_VERS -lmicocoss$MICO_VERS -lXt"
  else
    MICO_LIBS="-lmico$MICO_VERS -lmicogtk$MICO_VERS"
    AC_DEFINE(HAVE_MICO_ORB_RUN)
  fi

  for flag in `grep '^eval' < $mico_prefix/bin/mico-ld | sed s%\"%%g` ; do
    case $flag in
      -L*)
	MICO_LIBS="$flag $MICO_LIBS"
	;;
      -lm|-ldl|-ldld|-lbsd)
	;;
      -l*)
	MICO_LIBS="$MICO_LIBS $flag"
	;;
    esac
  done
  ORB_LIBS="$MICO_LIBS"
  ORB_CFLAGS="$MICO_CFLAGS"
fi
AC_SUBST(ORB_LIBS)
AC_SUBST(ORB_CFLAGS)

dnl gnome-session
dnl $GNOME_HAVE_SM comes from GNOME_X_CHECKS
AM_CONDITIONAL(SESSION, test "$GNOME_HAVE_SM" = true)
AC_ARG_WITH(window-manager,
[  --with-window-manager=NAME
			   Specify default window manager],[
    WINDOW_MANAGER="$with_window_manager"], [
    dnl icewm is the default because it supports session management
    dnl and other Gnome requirements.
    WINDOW_MANAGER=icewm])
AC_SUBST(WINDOW_MANAGER)

dnl Check for libgtop
GNOME_INIT_LIBGTOP

dnl applets/diskusage
GNOME_FILEUTILS_CHECKS

dnl panel/cdplayer
AC_CANONICAL_HOST
case "$host" in
*linux*) CDROM_HOST=linux;;
*solaris*) CDROM_HOST=solaris;;
esac
AM_CONDITIONAL(APPLET_CDPLAYER, test -n "$CDROM_HOST")
AC_SUBST(CDROM_HOST)

dnl panel/mixer
P_MIXER=
AC_CHECK_HEADERS(linux/soundcard.h,[P_MIXER=mixer],[
  AC_CHECK_HEADERS(machine/soundcard.h, [P_MIXER=mixer])])
AM_CONDITIONAL(APPLET_MIXER, test "$P_MIXER" = "mixer")

dnl panel/modemlights
P_MODEMLIGHTS=
AC_CHECK_HEADERS(net/ppp_defs.h,[P_MODEMLIGHTS=modemlights])
AM_CONDITIONAL(APPLET_MODEMLIGHTS, test "$P_MODEMLIGHTS" = "modemlights")
 
dnl help-browser
dnl I found `inet_aton' in -lresolv on solaris
oLIBS="$LIBS"
RES_LIBS=
# the following two are covered by X11 too.  If inet_aton is found in
# plain libc, -lsocket or -lnsl, RES_LIBS will be empty.
AC_CHECK_FUNC(connect,,[AC_CHECK_LIB(socket,connect)])
AC_CHECK_FUNC(gethostbyname,,[AC_CHECK_LIB(nsl,gethostbyname)])
AC_CHECK_FUNC(inet_aton,,[AC_CHECK_LIB(resolv,inet_aton,RES_LIBS="-lresolv")])
AC_SUBST(RES_LIBS)
LIBS="$oLIBS"

dnl AC_CONFIG_SUBDIRS(gemvt)

dnl gmenu requires ctree in the devel GTK 1.1, this is a simple check
dnl for gtk/gtkctree.h
P_GMENU=
AC_CHECK_HEADERS(gtk/gtkctree.h,[P_GMENU=gmenu])
AM_CONDITIONAL(PROGRAM_GMENU, test "$P_GMENU" = "gmenu")

AC_OUTPUT([
Makefile
po/Makefile.in
macros/Makefile
panel/Makefile
applets/Makefile
applets/applet-dirs/Makefile
applets/gen_util/Makefile
applets/batmon/Makefile
applets/bussign/Makefile
applets/cdplayer/Makefile
applets/mixer/Makefile
applets/modemlights/Makefile
applets/multiload/Makefile
applets/cpuload/Makefile
applets/netload/Makefile
applets/cpumemusage/Makefile
applets/fish/Makefile
applets/gkb/Makefile
applets/diskusage/Makefile
applets/icewm-pager/Makefile
applets/drivemount/Makefile
applets/clockmail/Makefile
applets/webcontrol/Makefile
applets/dialer/Makefile
applets/fifteen/Makefile
applets/battery/Makefile
applets/winlist/Makefile
core-docs/Makefile
desktop-links/Makefile
desktop-properties/Makefile
gnome-terminal/Makefile
help-browser/Makefile
help-browser/gnome-man2html/Makefile
help-browser/gnome-info2html/Makefile
gmenu/Makefile
control-center/Makefile
pixmaps/Makefile
gsm/Makefile
smproxy/Makefile
intl/Makefile],
[sed -e "/POTFILES =/r po/POTFILES" po/Makefile.in > po/Makefile])
