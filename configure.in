AC_INIT(desktop-properties)

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(gnome-core, 0.13)

AM_MAINTAINER_MODE

AM_ACLOCAL_INCLUDE(macros)

dnl
dnl let applications configure for gnome
dnl
gnome_cv_use_gnome=yes

GNOME_INIT

AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS
AC_ARG_PROGRAM
AM_PROG_LIBTOOL
AC_PROG_CXX
AC_PROG_LEX
AC_DECL_YYTEXT

GNOME_X_CHECKS

ALL_LINGUAS="it ko fr de"
AM_GNU_GETTEXT

AC_LINK_FILES($nls_cv_header_libgt, $nls_cv_header_intl)

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

AC_CHECK_HEADERS(dlfcn.h dl.h)
AC_CHECK_LIB(dl, dlopen, DL_LIB="-ldl",[
AC_CHECK_LIB(dld, shl_load, DL_LIB="-ldld",[
AC_CHECK_FUNCS(dlopen, DL_LIB="",
AC_MSG_ERROR(Dynamic linking is not available on this platform.  Some
apps, like panel, will not run properly.))
])])
AC_SUBST(DL_LIB)

dnl properties-keyboard
AC_CHECK_HEADERS(X11/extensions/xf86misc.h, XF86MISC_LIBS="-lXxf86misc")
AC_SUBST(XF86MISC_LIBS)

dnl gnome-session
dnl $GNOME_HAVE_SM comes from GNOME_X_CHECKS
AM_CONDITIONAL(SESSION, test "$GNOME_HAVE_SM" = true)

dnl panel/cdplayer
PROGRAMS_PANEL_CDPLAYER=""
AC_CANONICAL_HOST
case "$host" in
*linux*) CDROM_HOST=linux;;
*solaris*) CDROM_HOST=solaris;;
esac
test -n "$CDROM_HOST" && PROGRAMS_PANEL_CDPLAYER=cdplayer
AC_SUBST(CDROM_HOST)
AC_SUBST(PROGRAMS_PANEL_CDPLAYER)

dnl help-browser
dnl I found `inet_aton' in -lresolv on solaris
oLIBS="$LIBS"
RES_LIBS=
# the following two are covered by X11 too.  If inet_aton is found in
# plain libc, -lsocket or -lnsl, RES_LIBS will be empty.
AC_CHECK_FUNC(connect,,[AC_CHECK_LIB(socket,connect)])
AC_CHECK_FUNC(gethostbyname,,[AC_CHECK_LIB(nsl,gethostbyname)])
AC_CHECK_FUNC(inet_aton,,[AC_CHECK_LIB(resolv,inet_aton,RES_LIBS="-lresolv")])
AC_SUBST(RES_LIBS)
LIBS="$oLIBS"

AC_CONFIG_SUBDIRS(gemvt)

AC_OUTPUT([
Makefile
po/Makefile.in
macros/Makefile
panel/Makefile
applets/Makefile
applets/applet-dirs/Makefile
applets/clock/Makefile
applets/batmon/Makefile
applets/cdplayer/Makefile
applets/mailcheck/Makefile 
applets/netwatch/Makefile
desktop-links/Makefile
desktop-properties/Makefile
help-browser/Makefile
help-browser/gnome-man2html/Makefile
help-browser/gnome-info2html/Makefile
pixmaps/Makefile
gsm/Makefile
intl/Makefile],
[sed -e "/POTFILES =/r po/POTFILES" po/Makefile.in > po/Makefile])
